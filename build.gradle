plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
  id 'nu.studer.jooq' version '8.2'
}

group 'io.jacobking'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.9.2'
}

sourceCompatibility = '19'
targetCompatibility = '19'

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'io.jacobking.localticket'
  mainClass = 'io.jacobking.localticket.LocalTicket'
}

javafx {
  version = '19.0.2'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  implementation('org.controlsfx:controlsfx:11.1.2')
  implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.42.0.0'
  implementation group: 'com.zaxxer', name: 'HikariCP', version: '5.0.1'
  implementation 'commons-io:commons-io:2.13.0'
  implementation group: 'com.password4j', name: 'password4j', version: '1.7.1'
  implementation 'org.jooq:jooq:3.18.5'
  jooqGenerator 'org.xerial:sqlite-jdbc:3.42.0.0'
}

test {
  useJUnitPlatform()  }

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'app'
  }
}

jlinkZip {
  group = 'distribution'
}

import org.jooq.meta.jaxb.Logging

jooq {
  version = '3.18.4'  // default (can be omitted)
  edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

  configurations {
    main {  // name of the jOOQ configuration

      generationTool {
        logging = Logging.WARN
        jdbc {
          driver = 'org.sqlite.JDBC'
          url = 'jdbc:sqlite:C:\\Users\\JKING.SEATINGCONCEPTS\\IdeaProjects\\LocalTicket\\src\\main\\resources\\io\\jacobking\\localticket\\sql\\LocalTicket.db'
        }
        generator {
          name = 'org.jooq.codegen.DefaultGenerator'
          database {
            name = 'org.jooq.meta.sqlite.SQLiteDatabase'
          }
          generate {
            deprecated = false
            records = true
            immutablePojos = true
            fluentSetters = true
          }
          target {
            packageName = 'io.jacobking.jooq'
            directory = 'io.jacobking.jooq'  // default (can be omitted)
          }
          strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
        }
      }
    }
  }
}